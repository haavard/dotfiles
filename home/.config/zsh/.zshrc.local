# ---------------------
# .zshrc.local
#
# Using grml's zshrc
# https://grml.org/zsh/
# ---------------------

# try sourcing a file, silently ignoring if it doesn't exist
function source_if_exists { [ -e "$1" ] && source "$1" }

# Arch Linux syntax highlighting and command-not-found
source_if_exists \
    /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
source_if_exists /usr/share/doc/pkgfile/command-not-found.zsh

# Ubuntu syntax highlighting and command_not_found
source_if_exists /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
source_if_exists /etc/zsh_command_not_found

# store history in $XDG_DATA_HOME
HISTFILE="${XDG_DATA_HOME:-$HOME/.cache}/.zhistory"

# pass unmatched globs to the command, instead of failing
# e.g. "zsh: no matches found: HEAD^"
unsetopt nomatch

# don't correct or warn about completion internals (e.g. _wget)
CORRECT_IGNORE='_*'
zstyle ':acceptline:default' nocompwarn true

# use full hostname in prompt
zstyle ':prompt:grml:left:items:host' token '%M '

# add virtualenv to prompt
function virtual_env_prompt { REPLY=${VIRTUAL_ENV+(${VIRTUAL_ENV:t}) } }
if ! grml_theme_has_token virtual-env; then
    grml_theme_add_token virtual-env -f virtual_env_prompt '%F{magenta}' '%f'
fi
zstyle ':prompt:grml:left:setup' items \
    rc virtual-env change-root user at host path vcs percent

# use nvim as vim if available
if (( $+commands[nvim] )); then
    alias vim=nvim
    export EDITOR=nvim
fi

# configuration shortcuts
alias ez='$EDITOR '"${0:A} && source ${0:A}"
alias ev='$EDITOR ~/.vimrc'

# vimisms
alias :q='exit'
alias :e='$EDITOR'
function :h { vim "+help $@ | only" }
